
apply plugin: 'release'


buildscript {
  repositories {
     flatDir name: 'localRepository', dirs: 'gradle/lib'
     ivy { artifactPattern 'http://evgeny-goldin.org/artifactory/repo/[organisation]/[module]/[revision]/[artifact]-[revision].[ext]' }
  }

  dependencies {
    classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.3'
    classpath 'gradle-release:gradle-release:0.9.0'
  }
}


allprojects { project ->
    group = 'com.atoito.please'
    isSnapshot = version.endsWith("-SNAPSHOT")
    
    repositories {
        mavenLocal()
        mavenCentral()
    }

    javaModules = [
        ':modules:core', ':modules:cli', ':modules:acceptance-tests'
    ]
    
    acceptanceTests = [':modules:acceptance-tests']
}


subprojects {

    if (project.path in javaModules) {
        apply plugin: 'groovy'
        apply plugin: 'eclipse'
        apply plugin: 'license'

        configurations {
            compile.transitive = true
            testCompile.transitive = true
        }

        dependencies {
            groovy group: 'org.codehaus.groovy', name: 'groovy', version: '1.8.6'
            testCompile 'org.testng:testng:6.4',
                        'org.easytesting:fest-assert:1.4'
        }
        
        test {
            useTestNG()
        }
        
        archivesBaseName = "please-${project.name}"
        
        buildDir = 'target'

        sourceCompatibility = 1.5
        targetCompatibility = 1.5
        
        eclipse {
            project {
                name = "please-${project.name}"
            }
            classpath {
                // to keep the same depth in the build from in and outside Eclipse
                defaultOutputDir = new File("${project.buildDir}/eclipse/classes");
                downloadJavadoc = true
            }
        }
        
        task clean(type: Delete, overwrite: true) { task ->
            delete buildDir
            delete "build"
        }
    }

    if (project.path in acceptanceTests) {
        test.enabled = false
        task sut(   dependsOn: [':modules:core:test', ':modules:cli:test', ':modules:cli:installApp'],
                    description: 'Builds the system under test (using installApp).') << {
                    test.enabled = true
        }    
        task uat(   dependsOn: [sut, test], 
                    description: 'Runs the user acceptance tests.') << {
                    test.enabled = true
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.0-milestone-8a'
}

